const bottle = " bottle"
const plural = "s"
const ofbeer = " of beer"
const wall = " on the wall"
const sep = ", "
const takedown = "Take one down and pass it around, "
const u_no = "No"
const l_no = "no"
const more = " more bottles of beer"
const store = "Go to the store and buy some more, "
const dotnl = ".\n"
const nl = "\n"

// Write two digits, based on the value in a
fun writenum
    b = a
    loop
        break (a < 10)
        a /= 10
        // modulo is in the d register after idiv
        b = d
        a += 48 // ASCII value for '0'
        write(chr(a))
        break
    end
    a = b
    a += 48 // ASCII value for '0'
    write(chr(a))
end

fun main
    loop 99
        // Save loop counter for later, twice
        c -> stack
        c -> stack

        // Print the loop counter (passed in the a register)
        a = c
        writenum()

        // N, "bottles of beer on the wall, "
        write(bottle)
        write(plural)
        write(ofbeer)
        write(wall)
        write(sep)

        // Retrieve and print the number
        stack -> a
        writenum()

        // N, "bottles of beer."
        write(bottle)
        write(plural)
        write(ofbeer)
        write(dotnl)

        // "Take one down and pass it around,"
        write(takedown)

        // N-1, "bottles of beer on the wall."
        stack -> a
        a--
        // Store N-1, used just a few lines down
        a -> stack
        writenum()
        write(bottle)
        // Retrieve N-1
        stack -> a
        // Write an "s" if the count is not 1
        a != 1
            write(plural)
        end
        // Write the rest
        write(ofbeer)
        write(wall)
        write(dotnl)

        // Blank line
        write(nl)

        // Skip to the top of the loop while the counter is >= 2
        continue (c >= 2)

        // At the last two

        // "1 bottle of beer on the wall,"
        a = 1
        writenum()
        write(bottle)
        write(ofbeer)
        write(wall)
        write(sep)

        // "1 bottle of beer."
        a = 1
        writenum()
        write(bottle)
        write(ofbeer)
        write(dotnl)

        // "Take one down and pass it around,"
        write(takedown)

        // "no more bottles of beer on the wall."
        write(l_no)
        write(more)
        write(wall)
        write(dotnl)

        // Blank line
        write(nl)

        // "No more bottles of beer on the wall,"
        write(u_no)
        write(more)
        write(wall)
        write(sep)

        // "no more bottles of beer."
        write(l_no)
        write(more)
        write(dotnl)

        // "Go to the store and buy some more,"
        write(store)

        // "99 bottles of beer on the wall."
        a = 99
        writenum()
        write(bottle)
        write(plural)
        write(ofbeer)
        write(wall)
        write(dotnl)
    end
end


// vim: set syntax=c ts=4 sw=4 et:
