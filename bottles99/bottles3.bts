const bottle = " bottle"
const plural = "s"
const ofbeer = " of beer"
const wall = " on the wall"
const sep = ", "
const takedown = "Take one down and pass it around, "
const u_no = "No"
const l_no = "no"
const more = " more bottles"
const store = "Go to the store and buy some more, "
const nl = "\n"

// Write two digits, uses the value in a
fun writenum
    b = a
    loop
        break (a < 10)
        a /= 10
        // modulo is in the d register after idiv
        b = d
        a += 48 // ASCII value for '0'
        write(chr(a))
        break
    end
    a = b
    a += 48 // ASCII value for '0'
    write(chr(a))
end

fun main
    loop 99
        // Save loop counter for later, twice
        c -> stack
        c -> stack

        // Print the loop counter (passed in the a register)
        a = c
        writenum()

        // N, "bottles of beer on the wall, "
        string FOO
            bottle
            plural
            ofbeer
            wall
            sep
        end
        write(FOO)

        // Retrieve and print the number
        stack -> a
        writenum()

        // N, "bottles of beer."
        // "Take one down and pass it around,"
        string BOB
            bottle
            plural
            ofbeer
            dot
            nl
            takedown
        end
        write(BOB)

        // N-1, "bottles of beer on the wall."
        stack -> a
        a--
        // Store N-1, used just a few lines down
        a -> stack
        writenum()
        write(bottle)
        // Retrieve N-1
        stack -> a
        // Write an "s" if the count is not 1
        a != 1
            write(plural)
        end
        // Write the rest + blank line
        write(ofbeer + wall + dot + nl + nl)

        // Skip to the top of the loop while the counter is >= 2
        continue (c >= 2)

        // At the last two

        // "1 bottle of beer on the wall,"
        a = 1
        writenum()
        write(bottle + ofbeer + wall + sep)

        // "1 bottle of beer."
        // "Take one down and pass it around,"
        // "no more bottles of beer on the wall." + blank line
        // "No more bottles of beer on the wall,"
        // "no more bottles of beer."
        // "Go to the store and buy some more,"
        a = 1
        writenum()

        string ROGER
            bottle, ofbeer, dot, "\n"
            takedown, l_no, more, ofbeer, wall, dot, "\n"
            "\n"
            u_no, more, ofbeer, wall, sep, l_no, more, ofbeer, dot, "\n"
            store, 99, bottle, plural, ofbeer, wall, dot, "\n"
        end
        write(ROGER)
    end
end


// vim: set syntax=c ts=4 sw=4 et:
